plugins {
    id "groovy"
    id "com.gradle.plugin-publish" version "1.2.1"
}

version = '0.3.1'
group = 'io.github.qrtt1'

gradlePlugin {
    website = 'https://github.com/qrtt1/jib-gradle-reconfigure-plugin'
    vcsUrl = 'https://github.com/qrtt1/jib-gradle-reconfigure-plugin'
    plugins {
        greetingsPlugin {
            id = "${group}.jib"
            implementationClass = 'io.github.qrtt1.jib.JibReconfigurePlugin'
            displayName = 'Jib Reconfigure Plugin'
            description = 'Make jib reconfigurable from environment variables!'
            tags.set(['jib'])
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/qrtt1/jib-gradle-reconfigure-plugin/")
            credentials {
                username = project.findProperty("github.package.registry.username") ?: System.getenv("USERNAME")
                password = project.findProperty("github.package.registry.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'jib-gradle-reconfigure-plugin'
            version = version
            from(components.java)
            pom {
                name = "jib-gradle-reconfigure-plugin"
                description = "A Gradle plugin that allows you to dynamically reconfigure the Jib target image settings using environment variables."
                url = "https://github.com/qrtt1/jib-gradle-reconfigure-plugin"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "qrtt1"
                        name = "Ching Yi, Chan"
                        email = "chingyichan.tw@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/qrtt1/jib-gradle-reconfigure-plugin.git"
                    developerConnection = "scm:git:ssh://github.com/qrtt1/jib-gradle-reconfigure-plugin.git"
                    url = "https://github.com/qrtt1/jib-gradle-reconfigure-plugin"
                }
            }

        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation platform("org.spockframework:spock-bom:2.2-groovy-3.0")
    testImplementation 'org.spockframework:spock-core'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation gradleTestKit()
    testImplementation 'commons-io:commons-io:2.16.1'
}

test {
    useJUnitPlatform()
}